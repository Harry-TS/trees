apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.crickettechnology.trees"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 6
        versionName "1.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.0.0-beta01'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.gms:play-services-maps:16.0.0'
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    api 'com.google.guava:guava:27.0.1-android'
    implementation 'com.google.android.material:material:1.0.0'
}

task copyDrawable(type: Copy) {
    from '../../../assets'
    into 'src/main/res/drawable'

    include 'track_off@2x.png'
    include 'track_on@2x.png'
    include 'track_on2@2x.png'
    include 'info@2x.png'
    include 'close@2x.png'
    include 'compass@2x.png'
    include 'wikipedia@2x.png'
    include 'map@2x.png'
    include 'map_pin@2x.png'
    include 'info@2x.png'
    include 'tree@2x.png'

    rename '(.*)@2x(.*)', '$1$2' // remove ios "@2x"
}

task copyRaw(type: Copy) {
    from '../../../assets'
    into 'src/main/res/raw'

    include 'map_style.json'
}

task copyData(type: Copy) {
    from '../../../data'
    into 'src/main/assets'

    include 'datasets.json'
    include '**/site.bin'
    include '**/species.json'
}

preBuild.dependsOn copyDrawable
preBuild.dependsOn copyRaw
preBuild.dependsOn copyData
